// Scans the whole internet for all existing exploits to compile them into a readable database.

EOL = char(10)

SHELL = get_shell()
COMPUTER = SHELL.host_computer()
METAXPLOIT = include_lib("/lib/metaxploit.so")
DB_FILE = COMPUTER.File("/root/exploits.txt")
LAST_IP_FILE = COMPUTER.File("/root/last_ip.txt")
EXPLOITS = ""

get_between = function(string, c, d)
	a = string.indexOf(c) + len(c)
	b = string.indexOf(d)
	return string[a:b]
end function

analyze_port = function(port, conn, port_info)
	lib = conn.dump_lib
	zones = METAXPLOIT.scan(lib)
	for zone in zones
		vol_string = METAXPLOIT.scan_address(lib, zone)
		i = vol_string.indexOf("Unsafe check:")
		while i != null
			vol_string = vol_string[i+13:]
			unsec_value = get_between(vol_string, "<b>", "</b>")
			i = vol_string.indexOf("Unsafe check:")
			part = vol_string[:i]
			j = part.indexOf("*")
			reqs = ""
			while j != null
				part = part[j+1:]
				j = part.indexOf("*")
				req = part
				if j != null then
					req = part[:j]
				end if
				if req.indexOf("active user") != null then
					if req.indexOf("root") != null then
						reqs = reqs + "root_active|"
					else
						reqs = reqs + "users_active|"
					end if
				else if req.indexOf("registered users") != null then
					reqs = reqs + "reg_users:" + get_between(req, "to ", ".") + "|"
				else if req.indexOf("Using namespace") != null then
					a = get_between(req, "<b>", "</b>")
					b = get_between(req[req.indexOf("version"):], "<b>", "</b>")
					reqs = reqs + "namespace:" + a + ":" + b +"|"
				else if req.indexOf("port forwarding") != null then
					reqs = reqs + "ports_forwarded:" + get_between(req, " ", " port") + "|"
				else if req.indexOf("existing connection") != null then
					reqs = reqs + "lan" + "|"
				else
					print("<color=#ff0000><b>Requirement can't be parsed: </b></color>" + req)
				end if
			end while
			globals.EXPLOITS = port_info[0] + " " + port_info[1] + " " + port + " " + zone + " " + unsec_value + " " + reqs[:-1]
		end while
	end for
end function

main = function()
	db_content = DB_FILE.content
	last_ip = LAST_IP_FILE.content.split(".")

	for octet1 in range(last_ip[0].val, 255)
		for octet2 in range(last_ip[1].val, 255)
			for octet3 in range(last_ip[2].val, 255)
				for octet4 in range(last_ip[3].val, 255)
					router = null
					target_ip = str(octet1) + "." + str(octet2) + "." + str(octet3) + "." + str(octet4)
					router = get_router(target_ip)
					if not router then continue
					ports = router.used_ports()
					for port in ports
						globals.EXPLOITS = ""
						net_session = METAXPLOIT.net_use(target_ip, port.port_number)
						if not net_session then continue
						port_info = router.port_info(port).split(" ")
						analyze_port(port.port_number, net_session, port_info)
						for line in globals.EXPLOITS.split("\n")
							if db_content.indexOf(line) == null then
								db_content = db_content + "\n" + line
							end if
						end for
						DB_FILE.set_content(db_content)
					end for
					LAST_IP_FILE.set_content(target_ip)
				end for
			end for
		end for
	end for
end function

main()
